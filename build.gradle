buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
	}
}

plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id "me.modmuss50.mod-publish-plugin" version "0.5.1"
	id 'maven-publish'
}

apply plugin: 'maven-publish'
apply plugin: "me.modmuss50.mod-publish-plugin"

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

version = "${project.mod_version}+${project.minecraft_version}"
archivesBaseName = "${project.modid}"
group = project.maven_group

repositories {}
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE.txt"
}

apply from: 'gradle/maven.gradle'

String changelogText = file("CHANGELOG.md").getText('UTF-8')
Map<String, String> env = System.getenv()

publishMods {
	file = jar.archiveFile
	type = STABLE

	displayName = "CommandStructures-Fabric v${project.mod_version} (${project.minecraft_version} MC)"
	version = project.version + "_fabric"

	changelog = changelogText
	modLoaders.add("fabric")

	if (env.CURSEFORGEKEY) {
		curseforge {
			accessToken = env.CURSEFORGEKEY
			projectId = '565119'
			minecraftVersions.add("1.21")
			requires("fabric-api")
		}
	}

	if (env.MODRINTH_TOKEN) {
		modrinth {
			accessToken = env.MODRINTH_TOKEN
			projectId = 'WEfvvlnl'
			minecraftVersions.add("1.21")
			requires("fabric-api")
		}
	}
}

task buildAndRelease {}
tasks.publish.mustRunAfter tasks.build
tasks.publishMods.mustRunAfter tasks.publish
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishMods)
